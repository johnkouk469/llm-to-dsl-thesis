// Define the Metadata
Metadata
    name: SimpleHomeAutomation
    version: "0.1.0"
    description: "Automation to set the aircondition in the bedroom to 18 degrees on the cool mode when it is already on and the enviroment temperature is above 30 degrees."
    author: "the author"
    email: "the.author@gmail.com"
end

// Define the RT Monitor
RTMonitor
    broker: home_broker
    namespace: "smauto.simple_home_auto"
    eventTopic: "event"
    logsTopic: "logs"
end

// Define the MQTT broker
Broker<MQTT> home_broker
    host: "155.207.19.66"
    port: 1883  // Change this accordingly
    auth:
        username: "r4a"
        password: "r4a123$"
end

// Define the weather station entity 
Entity weather_station
    type: sensor
    freq: 5
    topic: "bedroom.weather_station"
    broker: home_broker
    attributes:
        - temperature: float -> gaussian(10, 35, 5) with noise gaussian(1,1)
        - humidity: float -> linear(1, 0.2) with noise uniform (0, 1)
        - pressure: float -> constant(0.5)
end

// Define the aircondition entity
Entity aircondition
    type: actuator
    topic: "bedroom.aircondition"
    broker: home_broker
    attributes:
        - temperature: float
        - mode: str
        - on: bool
end

// Automation to set the aircondition to 18 degrees on the cool mode when it is already on and the enviroment temperature is above 30 degrees
Automation start_aircondition
    condition:
        (weather_station.temperature > 30) AND
        (aircondition.on is true)
    enabled: true
    continuous: false
    actions:
        - aircondition.temperature:  18.0
        - aircondition.mode:  "cool"
end
